
composite blocktype FiguresExtractor {
    property url oftype text;

    // Input and outputs
    input inputName oftype None;
    output outputName oftype Sheet;

    // Pipeline definition from input, over blocks defined later, to output
    inputName
        ->FileExtractor
        ->FileXLSXInterpreter
        ->FiguresSheetPicker
        ->TableSelector
        ->outputName;

    // Block definitions using values from properties by name
    block FileExtractor oftype HttpExtractor { url: url; }
    block FileXLSXInterpreter oftype XLSXInterpreter {}
    block FiguresSheetPicker oftype SheetPicker { sheetName: 'Figure S5.1.2'; }
    block TableSelector oftype CellRangeSelector { select: range P2:S45; }
}


pipeline FiguresPipeline {


    FiguresExtractor 
        // Do BondIssuance
        -> BondIssuanceDeleter
        -> BondIssuanceTableInterpreter
        -> BondIssuanceLoader
        ;

    FiguresExtractor 
        // Do GdpPerCapita
        -> GdpPerCapitaDeleter
        -> GdpPerCapitaTableInterpreter
        -> GdpPerCapitaLoader
        ;

block FiguresExtractor oftype FiguresExtractor {
    url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
}

block BondIssuanceDeleter oftype ColumnDeleter {
   delete: [column B, column C];
}

block GdpPerCapitaDeleter oftype ColumnDeleter {
   delete: [column B, column D];
}



block BondIssuanceTableInterpreter oftype TableInterpreter {
   header: false;
   columns: [
     "Country Code" oftype CountryCodeAlpha3,
     "Bond Issuance Share" oftype ZeroOneInterval
   ];
} 

block GdpPerCapitaTableInterpreter oftype TableInterpreter {
   header: false;
   columns: [
    "Country Code" oftype CountryCodeAlpha3,
    "GDP per Capita" oftype decimal,
   ];
} 

// Validate rows
    valuetype ZeroOneInterval oftype decimal {
        constraints: [ZeroOneIntervalConstraint];
    }

    constraint ZeroOneIntervalConstraint on decimal: value >= 0 and value <= 1;


block BondIssuanceLoader oftype SQLiteLoader {
    table: "bondIssuance";
    file: "./country-stats.sqlite";
}

block GdpPerCapitaLoader oftype SQLiteLoader {
    table: "gdpPerCapita";
    file: "./country-stats.sqlite";
}

}